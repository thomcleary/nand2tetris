// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    Mux16(a=prev, b=prevInc, sel=inc, out=prevOrInc);
    Mux16(a=prevOrInc, b=in, sel=load, out=prevOrIncOrIn);
    Mux16(a=prevOrIncOrIn, b=false, sel=reset, out=registerIn);
    Register(in=registerIn, load=true, out=out, out=prev);
    Inc16(in=prev, out=prevInc);
}
